plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
}

group 'com.szklimek.functions.scrapper'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    invoker
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "com.google.cloud.functions:functions-framework-api:1.0.4"
    implementation 'com.google.cloud:google-cloudevent-types:0.3.0'

    // To run function locally using Functions Framework's local invoker
    invoker "com.google.cloud.functions.invoker:java-function-invoker:1.1.0"

}

// Register a "runFunction" task to run the function locally
tasks.register("runFunction", JavaExec) {
    main = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            '--target', project.findProperty('run.functionTarget') ?: '',
            '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}